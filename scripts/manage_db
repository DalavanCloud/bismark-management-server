#!/usr/bin/perl

use strict;
use warnings;
use DBI;
use Time::Local;

sub doPrompt($$);
sub doEasyListPrompt($@);
sub doHardListPrompt($@);

sub menuMain();
sub menuAddUser();
sub menuShowUsers();
sub menuModifyUser();
sub menuAddDevice();
sub menuModifyDevice();
sub menuExit();

sub extractMenuChoices(@);
sub dispatchFromMenuChoice($@);

sub catchSIGINT();

# Disable stdout buffering
$| = 1;

our ($sigint_called) = 0;

# Database setup information
#
my $DB_HOST="localhost";

my $DB_NAME="bismark_live_v1";
my $DB_USER="root";
my $DB_PASS="";

#my $DB_NAME="bismark_dev_v1";
#my $DB_USER="bismark-dev";
#my $DB_PASS="HereIsARandomDevPass4BMD";

our $dbh = DBI->connect("DBI:mysql:database=$DB_NAME;host=$DB_HOST", $DB_USER, $DB_PASS, { AutoCommit => 1 } )
        or die $DBI::errstr;

# Setup SIG INT handler
$SIG{'INT'} = \&catchSIGINT;

# Place to store some stateful information between functions
our %state = ();

menuMain();

$dbh->disconnect;

exit;

sub menuMain (){
	# Dispatch list, each list has the prompt we want for the menu and the function to
	# call if that option is chosen.
	my @menuDescription = (	['Add a user', \&menuAddUser],
				['Modify a user', \&menuModifyUser],
				['Show current users', \&menuShowUsers],
				['Add a device', \&menuAddDevice],
				['Modify a device', \&menuModifyDevice],
				['Quit', \&menuExit],
			);
	my $option;

	while (1){
		# Present the menu and grab the index of the user's choice
		$option = doEasyListPrompt("\n== MAIN MENU ==\n", @menuDescription);

		# Dispatch to new function
		$menuDescription[$option][1]();

		# Clear any state
		%state = ();
	}

	return;
}


sub menuAddUser(){
	my @menuDescription = (	['Name', \&doPrompt, 'User\'s name', ''],
				['Email', \&doPrompt, 'User\'s email', ''],
				['Address', \&doPrompt, 'User\'s home address', ''],
				['Phone', \&getPhone, 'User\'s phone number (###-###-####)', ''],
				['SLA', \&menuAssociateSLA, '', ''],
				['SLA Start Time', \&getTime, 'Please enter the time you want the SLA to begin its association (mm/dd/yyyy hh:mm or unixts)',time()],
				['Device', \&menuAssociateDevice, '', ''],
				['Device Start Time', \&getTime, 'Please enter the time you want the device to begin its association (mm/dd/yyyy hh:mm or unixts)',time()],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);	

	my $verified = 0;
	my $option;

	do {
		$option = doHardListPrompt("\n== ADD USER ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq 'Exit without saving changes'){
			return;
		}

		$verified = 1;
		for (my $i = 0; $i <= $#menuDescription; $i++){

			if (defined $menuDescription[$i][3] and $menuDescription[$i][3] eq ''){
				$verified = 0;
				print "ERROR: ", $menuDescription[$i][0], " not entered\n";
			}
		}


	} until ($verified);

	#
	# INSERT USER RECORD
	#
	my $insert = "INSERT INTO USERS (name, email, address, phone) VALUES (?,?,?,?)";
	my $sth = $dbh->prepare($insert)
		or die $dbh->errstr;

	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $menuDescription[2][3]);
	$sth->bind_param(4, $menuDescription[3][3]);

	$sth->execute()
		or die "error during insert: " . $dbh->errstr;
	
	my $userid = $dbh->{"mysql_insertid"};

	$sth->finish();

	#
	# INSERT USERSLA MAPPING
	#
	$insert = "INSERT INTO USERSLA (userid, slaid, start) VALUES (?,?,?)";
	$sth = $dbh->prepare($insert)
		or die $dbh->errstr;
	
	$sth->bind_param(1, $userid);
	$sth->bind_param(2, $menuDescription[4][3]);
	$sth->bind_param(3, $menuDescription[5][3]);

	$sth->execute()
		or die "error during insert: " . $dbh->errstr;
	$sth->finish();

	#
	# INSERT USERDEVICE MAPPING
	#
	$insert = "INSERT INTO USERDEVICE (userid, deviceid, start) VALUES (?,?,?)";
	$sth = $dbh->prepare($insert)
		or die $dbh->errstr;
	
	$sth->bind_param(1, $userid);
	$sth->bind_param(2, $menuDescription[6][3]);
	$sth->bind_param(3, $menuDescription[7][3]);

	$sth->execute()
		or die "error during insert: " . $dbh->errstr;
	$sth->finish();

}

sub menuModifyUser(){
	# 
	# First, figure out what user they want to modify
	# 
	my $query = "SELECT name,userid FROM USERS ORDER BY name";
	my @menuDescription = ();

	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	while (my $hash_ref = $sth->fetchrow_hashref) {
		push @menuDescription, [$hash_ref->{name} . " (userid: " . $hash_ref->{userid} . ")"];
	}
	push @menuDescription, ["Exit without selecting a user"];

	$sth->finish;

	my $option = doEasyListPrompt("== SELECT USER ==", @menuDescription);

	if ($menuDescription[$option][0] eq "Exit without selecting a user"){
		return;
	}

	# Store the userid in state (may be used by subfunctions)
	if ($menuDescription[$option][0] =~ /\(userid: (\d+)\)/){
		$state{userid} = $1;
	} else {
		die "error: unable to match userid in choice\n";
	}

	#
	# Pull this particular user's information.
	#
	$query = "SELECT name,email,address,phone,userid FROM USERS WHERE userid = " .  $state{userid};
	$sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	my $user_info = $sth->fetchrow_hashref;
	$sth->finish;

	$query = "SELECT slaid,start,end FROM USERSLA WHERE userid = " .  $state{userid} . " ORDER BY end is not null, end desc";
	$sth = $dbh->prepare($query)
		or die $dbh->errstr;
	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	my $sla_info = $sth->fetchrow_hashref;
	$state{slaid} = $sla_info->{slaid};
	$sth->finish;

	my $orig_slaid = $sla_info->{slaid};

	$query = "SELECT deviceid,start,end FROM USERDEVICE WHERE userid = " .  $state{userid} . " ORDER BY end is not null, end desc";
	$sth = $dbh->prepare($query)
		or die $dbh->errstr;
	$sth->execute()
		or die "error during select: " . $dbh->errstr;
	my $dev_info = $sth->fetchrow_hashref;
	$state{deviceid} = $dev_info->{deviceid};
	$sth->finish;

	my $orig_deviceid = $sla_info->{deviceid};

	# Makes sure data is consistent..
	checkForAssociatedDevice();
	checkForAssociatedSLA();

	#
	# Give them the ability to modify the data
	#
	@menuDescription = (	['Name', \&doPrompt, 'User\'s name', $user_info->{name}],
				['Email', \&doPrompt, 'User\'s email', $user_info->{email}],
				['Address', \&doPrompt, 'User\'s home address', $user_info->{address}],
				['Phone', \&getPhone, 'User\'s phone number (###-###-####)', $user_info->{phone}],
				['SLA', \&menuAssociateSLA, '', $sla_info->{slaid}],
				['SLA Dates', \&menuSLADates, '', 'submenu'],
				['Device', \&menuAssociateDevice, '', $dev_info->{deviceid}],
				['Device Dates', \&menuDeviceDates, '', 'submenu'],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);

	my $verified = 0;

	do {
		$option = doHardListPrompt("\n== MODIFY USER ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq "Exit without saving changes"){
			return;
		}

		$verified = 1;
		for (my $i = 0; $i <= $#menuDescription; $i++){
			if (defined $menuDescription[$i][3] and $menuDescription[$i][3] eq ''){
				$verified = 0;
				print "ERROR: ", $menuDescription[$i][0], " not entered\n";
			}
		}

	} until ($verified);

	#
	# Save the data back to the database
	#
	$query = "UPDATE USERS SET name = ?, email = ?, address = ?, phone = ? WHERE userid = ?";
	$sth = $dbh->prepare($query)
		or die $dbh->errstr;
	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $menuDescription[2][3]);
	$sth->bind_param(4, $menuDescription[3][3]);
	$sth->bind_param(5, $user_info->{userid});

	$sth->execute()
		or die "error during update: " . $dbh->errstr;
	$sth->finish;

}

sub menuShowUsers(){

	my $query = "SELECT name FROM USERS ORDER BY name";

	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	print "\nCurrent users in database:\n";
	while (my $hash_ref = $sth->fetchrow_hashref) {
		print $hash_ref->{name}, "\n";

	}

	print "\n";

	$sth->finish;

	return;
}

sub menuAssociateSLA(){
	my %sla;
	my @menuDescription = ();
	my ($default, $prompt) = @_;

	# Prevent continuing if the user already has an associated SLA
	if (checkForAssociatedSLA()){
		warn "Please set an end date for the currently associated SLA before attempting to change to a new SLA.\n";
		return $default;
	}

	# Pull all available SLA info down
	my $query = "SELECT slaid,isp,sla,dl,ul FROM SLA ORDER BY isp, sla";
	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;
	$sth->execute()
		or die "error during select: " . $dbh->errstr;


	push @menuDescription, ["Add a new SLA"];
	while (my $hash_ref = $sth->fetchrow_hashref) {
		my $desc = $hash_ref->{isp} . " " . $hash_ref->{sla} . " dl: " . $hash_ref->{dl} . " ul: " . $hash_ref->{ul} . " (slaid " . $hash_ref->{slaid} . ")";
		$sla{$desc} = $hash_ref->{slaid};
		push @menuDescription, [$desc];
	}
	push @menuDescription, ["Exit without selecting a SLA"];

	$sth->finish;

	my $option = doEasyListPrompt("== SELECT SLA ==", @menuDescription);

	my $newSLA;
	if ($menuDescription[$option][0] eq "Exit without selecting a SLA"){
		return $default;
	} elsif ($menuDescription[$option][0] eq "Add a new SLA"){
		$newSLA = menuAddSLA();
	} else {
		$newSLA = $sla{$menuDescription[$option][0]};
	}

	$state{slaid} = $newSLA if defined $newSLA;

	if (defined $state{userid}){
		$query = "INSERT INTO USERSLA (userid, slaid, start, end) VALUES (?,?,?,?)";
		$sth = $dbh->prepare($query)
			or die $dbh->errstr;
		$sth->bind_param(1, $state{userid});
		$sth->bind_param(2, $newSLA);
		$sth->bind_param(3, time());
		$sth->bind_param(4, undef);

		$sth->execute()
			or die "error during select: " . $dbh->errstr;
		$sth->finish;
	}

	return $newSLA;
}

sub menuSLADates(){

	my $sla_info;
	if (defined $state{userid}){
		my $query = "SELECT slaid,start,end FROM USERSLA WHERE userid = " .  $state{userid} . " ORDER BY end is not null, end desc";
		my $sth = $dbh->prepare($query)
			or die $dbh->errstr;

		$sth->execute()
			or die "error during select: " . $dbh->errstr;

		$sla_info = $sth->fetchrow_hashref;
	
		$sla_info->{end} = '' if not defined $sla_info->{end};
		$sth->finish;
	}

	# Fill in default info if we don't have an associated user/sla
	if (not defined $sla_info){
		$sla_info->{start} = time();
		$sla_info->{end} = '';
	}

	my @menuDescription = ( ['SLA Start Time', \&getTime, 'Please enter the time you want the SLA to begin its association (mm/dd/yyyy hh:mm or unixts)', $sla_info->{start}],
				['SLA End Time', \&getTime, 'Please enter the time you want the SLA to end its association (mm/dd/yyyy hh:mm or unixts)', $sla_info->{end}],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);

	my $verified = 0;
	my $option;

	do {
		$option = doHardListPrompt("\n== SLA DATES ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq 'Exit without saving changes'){
			return;
		}

		$verified = 1;
		if (defined $menuDescription[0][3] and $menuDescription[0][3] eq ''){
			$verified = 0;
			print "ERROR: ", $menuDescription[0][0], " not entered\n";
		}
		if ($menuDescription[1][3] eq ''){
			$menuDescription[1][3] = undef;
		}

	} until ($verified);

	my $update = "UPDATE USERSLA SET start = ?, end = ? WHERE userid = ? AND slaid = ? AND start = ?";
	my $sth = $dbh->prepare($update);

	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $state{userid});
	$sth->bind_param(4, $state{slaid});
	$sth->bind_param(5, $sla_info->{start});
	$sth->execute()
		or die "error during update: " . $dbh->errstr;

	return 'submenu';
}
sub menuDeviceDates(){
		
	my $dev_info;
	if (defined $state{userid}){
		my $query = "SELECT deviceid,start,end FROM USERDEVICE WHERE userid = " .  $state{userid} . "  ORDER BY end is not null, end desc";
		my $sth = $dbh->prepare($query)
			or die $dbh->errstr;
		$sth->execute()
			or die "error during select: " . $dbh->errstr;
		$dev_info = $sth->fetchrow_hashref;
		$dev_info->{end} = '' if not defined $dev_info->{end};
		$sth->finish;
	}

	# Fill in default info if we don't have an associated user/dev
	if (not defined $dev_info){
		$dev_info->{start} = time();
		$dev_info->{end} = '';
	}

	my @menuDescription = ( ['Device Start Time', \&getTime, 'Please enter the time you want the device to begin its association (mm/dd/yyyy hh:mm or unixts)', $dev_info->{start}],
				['Device End Time', \&getTime, 'Please enter the time you want the device to end its association (mm/dd/yyyy hh:mm or unixts)', $dev_info->{end}],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);

	my $verified = 0;
	my $option;

	do {
		$option = doHardListPrompt("\n== DEVICE DATES ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq 'Exit without saving changes'){
			return;
		}

		$verified = 1;
		if (defined $menuDescription[0][3] and $menuDescription[0][3] eq ''){
			$verified = 0;
			print "ERROR: ", $menuDescription[0][0], " not entered\n";
		}
		if ($menuDescription[1][3] eq ''){
			$menuDescription[1][3] = undef;
		}

	} until ($verified);

	my $update = "UPDATE USERDEVICE SET start = ?, end = ? WHERE userid = ? AND deviceid = ? AND start = ?";
	my $sth = $dbh->prepare($update);

	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $state{userid});
	$sth->bind_param(4, $state{deviceid});
	$sth->bind_param(5, $dev_info->{start});
	$sth->execute()
		or die "error during update: " . $dbh->errstr;

	return 'submenu';
}
	
sub menuAddSLA(){
	my @menuDescription = (	['ISP', \&doPrompt, 'ISP\'s name', ''],
				['SLA Name', \&doPrompt, 'SLA\'s name', ''],
				['Download speed', \&doPrompt, 'Download speed in Kbit/sec', ''],
				['Upload speed', \&doPrompt, 'Upload speed in Kbit/sec', ''],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);	

	my $verified = 0;
	my $option;

	do {
		$option = doHardListPrompt("\n== ADD SLA ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq 'Exit without saving changes'){
			return;
		}

		$verified = 1;
		for (my $i = 0; $i <= $#menuDescription; $i++){

			if (defined $menuDescription[$i][3] and $menuDescription[$i][3] eq ''){
				$verified = 0;
				print "ERROR: ", $menuDescription[$i][0], " not entered\n";
			}
		}


	} until ($verified);

	my $insert = "INSERT INTO SLA (isp, sla, dl, ul) VALUES (?,?,?,?)";
	my $sth = $dbh->prepare($insert);

	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $menuDescription[2][3]);
	$sth->bind_param(4, $menuDescription[3][3]);

	$sth->execute()
		or die "error during insert: " . $dbh->errstr;

	return $dbh->{"mysql_insertid"};
}

sub menuAssociateDevice(){
reprompt:
	my %devices;
	my %owners;
	my @menuDescription = ();
	my ($default, $prompt) = @_;

	# Prevent continuing if the user already has an associated SLA
	if (checkForAssociatedDevice()){
		warn "Please set an end date for the currently associated device before attempting to change to a new device.\n";
		return $default;
	}

	# Pull down all the device info
	my $query = "SELECT d.deviceid, d.type, d.os, d.version, d.macid, u.name, u.userid"
			. " FROM DEVICES d LEFT OUTER JOIN USERDEVICE ud ON d.deviceid=ud.deviceid AND ud.end IS NULL"
				. " LEFT JOIN USERS u ON ud.userid = u.userid"
			. " ORDER BY d.deviceid";
	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;
	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	push @menuDescription, ["Add a new device"];
	while (my $hash_ref = $sth->fetchrow_hashref) {

		my $desc = $hash_ref->{deviceid} . ": " . $hash_ref->{type} . " running " . $hash_ref->{os} . " version " . $hash_ref->{version};

		if (defined $hash_ref->{name}){
			$desc .= " (Currently assigned to " . $hash_ref->{name} . ")";
			$owners{$hash_ref->{deviceid}} = $hash_ref->{userid};
		} else {
			$desc .= " (unassigned)";
		}
		
		$devices{$desc} = $hash_ref->{deviceid};
		push @menuDescription, [$desc];
	}
	push @menuDescription, ["Exit without selecting a device"];

	$sth->finish;

	my $option = doEasyListPrompt("== SELECT DEVICE ==", @menuDescription);

	my $newDevice;
	if ($menuDescription[$option][0] eq "Exit without selecting a device"){
		return $default;
	} elsif ($menuDescription[$option][0] eq "Add a new device"){
		$newDevice = menuAddDevice();
	} else {
		$newDevice = $devices{$menuDescription[$option][0]};
		if (exists $owners{$newDevice}){
			my $userID = $owners{$newDevice};
			my $confirmation;
			do {
				$confirmation = doPrompt('no', "Please verify (yes/no) that you wish to reassign " . $newDevice. " from its current user " . $userID);
			} until ($confirmation =~ /^(?:yes)|(?:no)$/i);
			
			goto reprompt if ($confirmation =~ /no/i);

			my $endtime = getTime(time(), 'Please enter the time you want the device to end its association with the old user (mm/dd/yyyy hh:mm or unixts)');

			my $update = "UPDATE USERDEVICE SET end = ? WHERE userid = ? AND deviceid = ? AND end IS NULL";
			my $update_sth = $dbh->prepare($update);

			$update_sth->bind_param(1, $endtime);
			$update_sth->bind_param(2, $userID);
			$update_sth->bind_param(3, $newDevice);
			$update_sth->execute()
				or die "error during update: " . $dbh->errstr;
		}
	}
	$state{deviceid} = $newDevice if defined $newDevice;

	if (defined $state{userid}){
		$query = "INSERT INTO USERDEVICE (userid, deviceid, start, end) VALUES (?,?,?,?)";
		$sth = $dbh->prepare($query)
			or die $dbh->errstr;
		$sth->bind_param(1, $state{userid});
		$sth->bind_param(2, $newDevice);
		$sth->bind_param(3, time());
		$sth->bind_param(4, undef);

		$sth->execute()
			or die "error during insert: " . $dbh->errstr;
	}
	return $newDevice;
}

sub menuAddDevice(){
	my @menuDescription = (	['Device Name', \&doPrompt, 'Device\'s name', ''],
				['Device Type', \&menuSelectDeviceType, '', ''],
				['OS Type', \&menuSelectOSType, '', ''],
				['OS Version', \&doPrompt, 'OS\'s version', ''],
				['MAC Address', \&getMACAddress, 'Device\'s MAC address', ''],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);	

	my $verified = 0;
	my $option;

	do {
		$option = doHardListPrompt("\n== ADD DEVICE ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq 'Exit without saving changes'){
			return;
		}

		$verified = 1;
		for (my $i = 0; $i <= $#menuDescription; $i++){

			if (defined $menuDescription[$i][3] and $menuDescription[$i][3] eq ''){
				$verified = 0;
				print "ERROR: ", $menuDescription[$i][0], " not entered\n";
			}
		}

	} until ($verified);

	my $insert = "INSERT INTO DEVICES (deviceid, type, os, version, macid) VALUES (?,?,?,?,?)";
	my $sth = $dbh->prepare($insert);

	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $menuDescription[2][3]);
	$sth->bind_param(4, $menuDescription[3][3]);
	$sth->bind_param(5, $menuDescription[4][3]);

	$sth->execute()
		or die "error during insert: " . $dbh->errstr;

	return $menuDescription[0][3];
}

sub getMACAddress {
	my ($default, $prompt) = @_;
	my $macAddr;
	my $verified = 0;

	do {
		$macAddr = doPrompt($default, $prompt);
	
		if ($macAddr =~ /^(?:[0-9a-f]{2}:){5}[0-9a-f]{2}$/i or
	  	    $macAddr =~ /^[0-9a-f]{12}$/i or 
 		    $macAddr eq "Unknown"){
			$verified = 1;
		} else {
			print "ERROR: MAC address does not appear properly formatted (xx:xx:xx:xx:xx:xx)\n";
		}
	} until ($verified);
	
	return $macAddr;
}

sub getPhone {
	my ($default, $prompt) = @_;
	my $phone;
	my $verified = 0;

	do {
		$phone = doPrompt($default, $prompt);
	
		if ($phone =~ /^(?:\d\d\d-\d\d\d-\d\d\d\d)$/i or
		    $phone eq "Unknown"){
			$verified = 1;
		} else {
			print "ERROR: phone number does not appear to be properly formatted (###-###-####)\n";
		}
	} until ($verified);
	
	return $phone;
}

sub getTime {
	my ($default, $prompt) = @_;
	my $time;
	my $verified = 0;

	do {
		$time = doPrompt($default, $prompt);
	
		if ($time =~ /^\d+$/){
			$verified = 1;
		} elsif ($time =~ /^(\d+)\/(\d+)\/(\d+) (\d+):(\d+)$/){
			$time = timelocal(0, $5, $4, $2, $1-1, $3);
			print "Date converted to " . scalar(localtime($time)) . ", please reenter if that is incorrect.\n";
			$verified = 1;
		} else {
			print "ERROR: Date does not look properly formatted (mm/dd/yy hh:mm or unix ts)\n";
		}
	} until ($verified);
	
	return $time;
}

sub menuSelectDeviceType {
	my ($default, $prompt) = @_;
	my @menuDescription = ();
	my $query = "SELECT DISTINCT type FROM DEVICES ORDER BY type";

	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	push @menuDescription, ["Add a new device type"];
	while (my $hash_ref = $sth->fetchrow_hashref) {
		push @menuDescription, [$hash_ref->{type}];
	}
	push @menuDescription, ["Exit without selecting a device type"];

	$sth->finish;

	my $option = doEasyListPrompt("== SELECT DEVICE TYPE ==", @menuDescription);

	if ($menuDescription[$option][0] eq "Exit without selecting a device type"){
		return $default;
	} elsif ($menuDescription[$option][0] eq "Add a new device type"){
		return doPrompt('', "Please enter the new device type you would like to use");
	} else {
		return $menuDescription[$option][0];
	}
}

sub menuSelectOSType {
	my @menuDescription = ();
	my ($default, $prompt) = @_;
	my $query = "SELECT DISTINCT os FROM DEVICES ORDER BY os";

	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	push @menuDescription, ["Add a new device OS"];
	while (my $hash_ref = $sth->fetchrow_hashref) {
		push @menuDescription, [$hash_ref->{os}];
	}
	push @menuDescription, ["Exit without selecting a device OS"];

	$sth->finish;

	my $option = doEasyListPrompt("== SELECT DEVICE OS ==", @menuDescription);

	if ($menuDescription[$option][0] eq "Exit without selecting a device OS"){
		return $default;
	} elsif ($menuDescription[$option][0] eq "Add a new device OS"){
		return doPrompt('', "Please enter the new device OS you would like to use");
	} else {
		return $menuDescription[$option][0];
	}
}

sub menuModifyDevice(){
	# 
	# First, figure out what user they want to modify
	# 
	my $query = "SELECT deviceid FROM DEVICES ORDER BY deviceid";
	my %deviceids;
	my @menuDescription = ();

	my $sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	while (my $hash_ref = $sth->fetchrow_hashref) {
		$deviceids{$hash_ref->{deviceid}} = 1;
		push @menuDescription, [$hash_ref->{deviceid}];
	}
	push @menuDescription, ["Exit without selecting a device"];

	$sth->finish;

	my $option = doEasyListPrompt("== SELECT DEVICE ==", @menuDescription);

	if ($menuDescription[$option][0] eq "Exit without selecting a device"){
		return;
	}

	#
	# Pull this particular device's information.
	#
	$query = "SELECT deviceid,type,os,version,macid FROM DEVICES WHERE deviceid = \"" .  $menuDescription[$option][0] . "\"";
	$sth = $dbh->prepare($query)
		or die $dbh->errstr;

	$sth->execute()
		or die "error during select: " . $dbh->errstr;

	my $dev_info = $sth->fetchrow_hashref;
	$sth->finish;

	#
	# Give them the ability to modify the data EXCEPT deviceid (primary key of the table.. changing
	# this is more complicated and not yet supported.)
	#
	@menuDescription = (    ['Device Type', \&menuSelectDeviceType, '', $dev_info->{type}],
				['OS Type', \&menuSelectOSType, '', $dev_info->{os}],
				['OS Version', \&doPrompt, 'OS\'s version', $dev_info->{version}],
				['MAC Address', \&getMACAddress, 'Device\'s MAC address', $dev_info->{macid}],
				['Exit without saving changes', , , ],
				['Save changes', , , ],
			);

	my $verified = 0;

	do {
		$option = doHardListPrompt("\n== MODIFY DEVICE ==\n", @menuDescription);

		if ($menuDescription[$option][0] eq 'Exit without saving changes'){
			return;
		}

		$verified = 1;
		for (my $i = 0; $i <= $#menuDescription; $i++){

			if (defined $menuDescription[$i][3] and $menuDescription[$i][3] eq ''){
				$verified = 0;
				print "ERROR: ", $menuDescription[$i][0], " not entered\n";
			}
		}

	} until ($verified);

	#
	# Save the data back to the database
	#
	$query = "UPDATE DEVICES SET type = ?, os = ?, version = ?, macid = ? WHERE deviceid= ?";
	$sth = $dbh->prepare($query);

	$sth->bind_param(1, $menuDescription[0][3]);
	$sth->bind_param(2, $menuDescription[1][3]);
	$sth->bind_param(3, $menuDescription[2][3]);
	$sth->bind_param(4, $menuDescription[3][3]);
	$sth->bind_param(5, $dev_info->{deviceid});

	$sth->execute()
		or die "error during update: " . $dbh->errstr;

}

# XXX: This is a hack. 
sub menuExit(){ exit; }

sub doEasyListPrompt ($@){
	my ($prompt, @menuDescription) = @_;
	my ($answer, $acceptableAnswer);
	my @choices = extractMenuChoices(@menuDescription);

	# do/until we get valid input 
	do { 
		print "$prompt\n";
	
		# provide prompt
		for (my $i = 0; $i <= $#choices; $i++){
			# Make choices start from 1 and go up to be userfriendly
			print $i+1, ") ", $choices[$i], "\n";
		}
		print "\nPlease enter your choice: ";
		
		# Get answer, can newline
		$answer = <>;
		chomp $answer;

		# Check for valid input
		if ($answer !~ /^\d+$/){
			$acceptableAnswer = 0;
		} elsif ($answer < 1 or $answer > ($#choices+1)){
			$acceptableAnswer = 0;
		} else {
			$acceptableAnswer = 1;
		}

		# Handle any errors
		if (!$acceptableAnswer){
			print "ERROR: that doesn't seem to be a valid choice; please try again.\n\n";
		}

	} until ($acceptableAnswer);

	# Return the user's answer as an index to the array they passed us
	return(($answer - 1));
}

sub doHardListPrompt ($@){
	my ($prompt, @menuDescription) = @_;
	my ($option, $curVal, $newVal);
	my ($commitAnswer) = 0;

	# Add the default/current answer bit to the description
	for (my $i = 0; $i <= $#menuDescription; $i++){
		if (defined $menuDescription[$i][3]){
			$menuDescription[$i][0] .= " [" . $menuDescription[$i][3] . "]";
		}
	}

        do {
                $option = doEasyListPrompt($prompt, @menuDescription);

                if (defined $menuDescription[$option][1]){
                        if ($menuDescription[$option][0] =~ /\[(.*?)\]/){
                                $curVal = $1;
                        } else {
                                $curVal = "";
                        }
                        $newVal = $menuDescription[$option][1]($curVal, $menuDescription[$option][2]);
                        $menuDescription[$option][0] =~ s/\[.*?\]/\[$newVal\]/g;
                        $menuDescription[$option][3] = $newVal;
                } else {
                        $commitAnswer = 1;
                }

        } until ($commitAnswer);

	# Remove the default/current answer bit from the description
	for (my $i = 0; $i <= $#menuDescription; $i++){
		$menuDescription[$i][0] =~ s/ \[.*?\]$//g;
	}

	return($option);
}

# doPrompt displays a user specified prompt with an offered default.
# returns what the user entered at the prompt.
sub doPrompt ($$){
	my ($default, $prompt) = @_;
	my $answer;

	# Provide prompt
	print "$prompt [$default]: ";

	# Get answer, can newline
	$answer = <>;
	chomp $answer;

	# Return the user's answer, handling default entry if necessary
	if ($answer eq ""){
		return $default;
	} else {
		return $answer;
	}
}

sub extractMenuChoices(@){
	my @menuDescription = @_;
	my @menuChoices = ();

	# Extract the first element (the choice's description) from the array
	# and put it in a new array. Return that array.
	for (my $i=0; $i <= $#menuDescription; $i++){
		push @menuChoices, @{ $menuDescription[$i] }[0];
	}
	return @menuChoices;
}

sub catchSIGINT(){
	if (time() - $sigint_called < 5){
		exit;
	}

	print "\nWARNING: Ctrl-C may cause unhandled behavior in this application. Hit it again quickly if you really want to exit.\n";

	$sigint_called = time();
}

sub checkForAssociatedDevice(){
	my $assocDevice = 0;

	# Don't let users with an active associated SLA select a new one.
	if (defined $state{userid}){

		my $query = "SELECT deviceid FROM USERDEVICE WHERE userid = " . $state{userid} . " AND end IS NULL";
		my $sth = $dbh->prepare($query)
			or die $dbh->errstr;

		$sth->execute()
			or die "error during select: " . $dbh->errstr;
		while (my $hash_ref = $sth->fetchrow_hashref) {
			if ($assocDevice == 1){
				die "error: userid " . $state{userid} . " has more than 1 unterminated associated device. Please fix manually before proceeding.\n";
			} else {
				$assocDevice = 1;
			}
		}
		$sth->finish;
	}

	return $assocDevice;
}

sub checkForAssociatedSLA(){
	my $assocSLA = 0;

	# Don't let users with an active associated SLA select a new one.
	if (defined $state{userid}){

		my $query = "SELECT slaid FROM USERSLA WHERE userid = " . $state{userid} . " AND end IS NULL";
		my $sth = $dbh->prepare($query)
			or die $dbh->errstr;

		$sth->execute()
			or die "error during select: " . $dbh->errstr;
		while (my $hash_ref = $sth->fetchrow_hashref) {
			if ($assocSLA == 1){
				die "error: userid " . $state{userid} . " has more than 1 unterminated associated SLA. Please fix manually before proceeding.\n";
			} else {
				$assocSLA = 1;
			}
		}
		$sth->finish;
	}

	return $assocSLA;
}

